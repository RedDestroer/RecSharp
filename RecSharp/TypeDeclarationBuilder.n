using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.CodeDom;
using System.Collections.Generic;
using System.Linq;
using System.Reflection;
using System.Runtime.InteropServices;

using RecSharp.Dom;
using RecSharp.Utils;

namespace RecSharp
{
  /// <summary>
  /// Builder for CodeTypeDeclaration
  /// </summary>
  class TypeDeclarationBuilder
  {
    private name : string;
    private propertyDefinitions : List[PropertyDefinition];
    
    public this(recDef : RecordDefinition) {
      this.name = recDef.TypeName;
      this.propertyDefinitions = recDef.Properties;
    }
    
    private BuildProperty(definition : PropertyDefinition) : CodeMemberField * CodeMemberProperty {
      def fieldName = "_" + definition.PropertyName.Minusculize();
      def typeRef = CodeTypeReference(definition.TypeName);
      def field = CodeMemberField(typeRef, fieldName) <- {
        Attributes = MemberAttributes.Private;
      };
      def property = CodeMemberProperty() <- {
        Name = definition.PropertyName;
        Type = typeRef;
        Attributes = MemberAttributes.Public | MemberAttributes.Final;
      };
      _ = property.GetStatements.Add(CodeSnippetStatement($"return this.$(fieldName);"));
      
      (field, property)
    }
    
    private BuildCopyMethod() : CodeMemberMethod {
      def methodDef = CodeMemberMethod() <- {
        Name = "Copy";
        ReturnType = CodeTypeReference(name);
        Attributes = MemberAttributes.Public | MemberAttributes.Final;
      }
      
      def ctorParams = array(propertyDefinitions.Count);
      
      foreach(propDef in propertyDefinitions with idx) {
        def argName = propDef.PropertyName.Minusculize();
        def fieldName = "_" + argName;
        def typeRef = CodeTypeReference(propDef.TypeName);
        def param = CodeParameterDeclarationExpression(typeRef, argName);
        _ = param.CustomAttributes.Add(CodeAttributeDeclaration(CodeTypeReference(typeof(OptionalAttribute))));
        _ = methodDef.Parameters.Add(param);
        
        ctorParams[idx] = CodeSnippetExpression($"$(argName) != default($(propDef.TypeName)) ? $(argName) : this.$(fieldName)");
      }
    
      def constructorCall = CodeObjectCreateExpression(name, ctorParams);
      _ = methodDef.Statements.Add(CodeMethodReturnStatement(constructorCall));
      
      methodDef
    }
    
    private BuildEqualsMethod() : CodeMemberMethod {
      def methodDef = CodeMemberMethod() <- {
        Name = "Equals";
        ReturnType = CodeTypeReference(typeof(bool));
        Attributes = MemberAttributes.Public | MemberAttributes.Override;
      };
      
      def argName = "other";
      def param = CodeParameterDeclarationExpression(CodeTypeReference(typeof(object)), argName);
      _ = methodDef.Parameters.Add(param);
        
      def bodyString = $"return this.Equals(other as $(name));";
      _ = methodDef.Statements.Add(CodeSnippetStatement(bodyString));  
        
       methodDef
    }
    
    private BuildSpecializedEqualsMethod() : CodeMemberMethod {
      def methodDef = CodeMemberMethod() <- {
        Name = "Equals";
        ReturnType = CodeTypeReference(typeof(bool));
        Attributes = MemberAttributes.Public | MemberAttributes.Final;
      };
      
      def argName = "other";
      def param = CodeParameterDeclarationExpression(CodeTypeReference(name), argName);
        _ = methodDef.Parameters.Add(param);
        
      _ = methodDef.Statements.Add(CodeSnippetStatement("if (other == null) return false;"));
      
      foreach(propDef in propertyDefinitions) {
       _ = methodDef.Statements.Add(CodeSnippetStatement($"if (!$(propDef.PropertyName).Equals(other.$(propDef.PropertyName))) return false;"));
      }
      
      _ = methodDef.Statements.Add(CodeMethodReturnStatement(CodePrimitiveExpression(true)));
      
      methodDef
    }
    
    private BuildEqualityOperator() : CodeMemberMethod {
      def methodDef = CodeMemberMethod() <- {
        Name = "operator ==";
        ReturnType = CodeTypeReference(typeof(bool));
        Attributes = MemberAttributes.Public | MemberAttributes.Static;
      };
      
      _ = methodDef.Parameters.Add(CodeParameterDeclarationExpression(CodeTypeReference(this.name), "left"));
      _ = methodDef.Parameters.Add(CodeParameterDeclarationExpression(CodeTypeReference(this.name), "right"));
        
      def bodyString = "return left != null && left.Equals(right);";
      _ = methodDef.Statements.Add(CodeSnippetStatement(bodyString));  
        
       methodDef
    }
    
    private BuildInequalityOperator() : CodeMemberMethod {
      def methodDef = CodeMemberMethod() <- {
        Name = "operator !=";
        ReturnType = CodeTypeReference(typeof(bool));
        Attributes = MemberAttributes.Public | MemberAttributes.Static;
      };
      
      _ = methodDef.Parameters.Add(CodeParameterDeclarationExpression(CodeTypeReference(this.name), "left"));
      _ = methodDef.Parameters.Add(CodeParameterDeclarationExpression(CodeTypeReference(this.name), "right"));
        
      def bodyString = "return left == null || !left.Equals(right);";
      _ = methodDef.Statements.Add(CodeSnippetStatement(bodyString));  
        
       methodDef
    }
    
    private BuildGetHashCode() : CodeMemberMethod {
      def methodDef = CodeMemberMethod() <- {
        Name = "GetHashCode";
        ReturnType = CodeTypeReference(typeof(int));
        Attributes = MemberAttributes.Public | MemberAttributes.Override;
      };
      
      def body = List() <- ["var hash = 17;"];
      
      foreach(propDef in propertyDefinitions) {
        body.Add($"if ($(propDef.PropertyName) != null) hash = hash * 23 + $(propDef.PropertyName).GetHashCode();");
      }
      
      body.Add("return hash;");
      
      def bodyString = string.Join("\n", body);
      _ = methodDef.Statements.Add(CodeSnippetStatement(bodyString));  
        
       methodDef
    }
    
    public Build() : CodeTypeDeclaration {
      def declaration = CodeTypeDeclaration(this.name) <- {
        IsClass = true;
        TypeAttributes = TypeAttributes.Public | TypeAttributes.Sealed;
      }
      
      _ = declaration.BaseTypes.Add(CodeTypeReference($"System.IEquatable`1[$(this.name)]"));
      
      foreach(propDef in propertyDefinitions) {
        def (field, property) = BuildProperty(propDef);
        _ = declaration.Members.Add(field);
        _ = declaration.Members.Add(property);
      }
      
      def ctor = CodeConstructor() <- {
        Attributes = MemberAttributes.Public;
      };
      
      foreach(propDef in propertyDefinitions) {
        def name = propDef.PropertyName.Minusculize();
        def fieldName = "_" + name;
        def typeRef = CodeTypeReference(propDef.TypeName);
        def param = CodeParameterDeclarationExpression(typeRef, name);
        _ = ctor.Parameters.Add(param);
        
        def assignment = CodeAssignStatement(
          CodeFieldReferenceExpression(CodeThisReferenceExpression(), fieldName),
          CodeArgumentReferenceExpression(name)
        );
        
        _ = ctor.Statements.Add(assignment);
      }
      
      _ = declaration.Members.Add(ctor);
      _ = declaration.Members.Add(BuildCopyMethod());
      _ = declaration.Members.Add(BuildEqualsMethod());
      _ = declaration.Members.Add(BuildSpecializedEqualsMethod());
      _ = declaration.Members.Add(BuildEqualityOperator());
      _ = declaration.Members.Add(BuildInequalityOperator());
      _ = declaration.Members.Add(BuildGetHashCode());
      
      declaration
    }
  }
}
